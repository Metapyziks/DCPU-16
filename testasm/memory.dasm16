			; Memory allocation test
			; By Metapyziks
			
.const memloc 	0xa000
.const meminit 	0xa001
.const memlen 	0x4000
.const memmask 	0x3fff
			
			SET [memloc], memlen
			SET PC, __skip__
:malloc
			SET PUSH, B
			SET PUSH, C
			SET PUSH, X
			SET PUSH, Y
			SET X, 0
:__malloc__loop
			IFG X, memmask
				SET PC, __malloc__fail
			SET B, [memloc+X]
			SET C, B
			AND C, 0x8000
			AND B, 0x7fff
			IFN C, 0x0000
				SET PC, __malloc__loopiter
			IFG A, B
				SET PC, __malloc__loopiter
			IFE A, B
				SET PC, __malloc__loopiter
			SET PC, __malloc__loopend
:__malloc__loopiter
			ADD X, B
			SET PC, __malloc__loop
:__malloc__loopend
			ADD A, 2
			IFG B, A
				SUB A, 1
			BOR A, 0x8000
			SET [memloc+X], A
			AND A, 0x7999
			ADD A, 1
			IFG A, B
				SET PC, __malloc__getaddr
			SUB A, 1
			SET Y, X
			ADD Y, A
			SUB B, A
			SET [memloc+Y], B
:__malloc__getaddr
			SET A, X
			ADD A, meminit
			SET PC, __malloc__return
:__malloc__fail
			SET A, 0xffff
:__malloc__return
			SET Y, POP
			SET X, POP
			SET C, POP
			SET B, POP
			SET PC, POP
:free
			SET PUSH, B
			SET PUSH, C
			SET PUSH, X
			SET PUSH, Y
			SET PUSH, Z
			SET X, 0
			SUB A, meminit
:__free__loop
			SET B, [memloc+X]
			AND B, 0x7fff
			SET Z, X
			ADD Z, B
			IFE X, A
				SET PC, __free__loopend
			IFG X, A
				SET PC, __free__return
			SET Y, X
			SET X, Z
			IFG memlen, X
				SET PC, __free__loop
			SET PC, __free__return
:__free__loopend
			IFG memlen, Z
				JSR __free__mergeright
			IFG X, 0
				JSR __free__mergeleft
			SET [memloc+X], B
			SET PC, __free__return
:__free__mergeright
			SET C, [memloc+Z]
			AND C, 0x8000
			IFE C, 0x0000
				ADD B, [memloc+Z]
			SET PC, POP
:__free__mergeleft
			SET C, [memloc+Y]
			AND C, 0x8000
			IFN C, 0x0000
				SET PC, POP
			ADD B, [memloc+Y]
			SET X, Y
			SET PC, POP
:__free__return
			SET Z, POP
			SET Y, POP
			SET X, POP
			SET C, POP
			SET B, POP
			SET PC, POP
:memset
			SUB A, memloc
			ADD C, A
			SUB C, 1
:__memset__loop
			IFG A, C
				SET PC, POP
			SET [memloc+A], B
			ADD A, 1
			SET PC, __memset__loop
:__skip__
			