			; Memory allocation test
			; By Metapyziks
			
.define 	memloc 		0xa000
.define 	meminit 	0xa001
.define 	memlen 		0x4000
.define 	memmask 	0x3fff
			
			SET [memloc], memlen
			SET PC, __memory__skip__
:malloc
			SET PUSH, B
			SET PUSH, C
			SET PUSH, X
			SET PUSH, Y
			SET X, 0
::loop
			IFG X, memmask
				SET PC, fail
			SET B, [memloc+X]
			SET C, B
			AND C, 0x8000
			AND B, 0x7fff
			IFN C, 0x0000
				SET PC, loopiter
			IFG A, B
				SET PC, loopiter
			IFE A, B
				SET PC, loopiter
			SET PC, loopend
::loopiter
			ADD X, B
			SET PC, loop
::loopend
			ADD A, 2
			IFG B, A
				SUB A, 1
			BOR A, 0x8000
			SET [memloc+X], A
			AND A, 0x7999
			ADD A, 1
			IFG A, B
				SET PC, getaddr
			SUB A, 1
			SET Y, X
			ADD Y, A
			SUB B, A
			SET [memloc+Y], B
::getaddr
			SET A, X
			ADD A, meminit
			SET PC, return
::fail
			SET A, 0xffff
::return
			SET Y, POP
			SET X, POP
			SET C, POP
			SET B, POP
			SET PC, POP
:free
			SET PUSH, B
			SET PUSH, C
			SET PUSH, X
			SET PUSH, Y
			SET PUSH, Z
			SET X, 0
			SUB A, meminit
::loop
			SET B, [memloc+X]
			AND B, 0x7fff
			SET Z, X
			ADD Z, B
			IFE X, A
				SET PC, loopend
			IFG X, A
				SET PC, return
			SET Y, X
			SET X, Z
			IFG memlen, X
				SET PC, loop
			SET PC, return
::loopend
			IFG memlen, Z
				JSR mergeright
			IFG X, 0
				JSR mergeleft
			SET [memloc+X], B
			SET PC, return
::mergeright
			SET C, [memloc+Z]
			AND C, 0x8000
			IFE C, 0x0000
				ADD B, [memloc+Z]
			SET PC, POP
::mergeleft
			SET C, [memloc+Y]
			AND C, 0x8000
			IFN C, 0x0000
				SET PC, POP
			ADD B, [memloc+Y]
			SET X, Y
			SET PC, POP
::return
			SET Z, POP
			SET Y, POP
			SET X, POP
			SET C, POP
			SET B, POP
			SET PC, POP
:memset
			SUB A, memloc
			ADD C, A
			SUB C, 1
::loop
			IFG A, C
				SET PC, POP
			SET [memloc+A], B
			ADD A, 1
			SET PC, loop
:__memory__skip__
