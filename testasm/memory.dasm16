			; Memory allocation test
			; By Metapyziks
			
.define 	memloc 		0xa000
.define 	meminit 	0xa001
.define 	memlen 		0x4000
.define 	memmask 	0x3fff
			
			SET [memloc], memlen
			SET PC, __memory__skip__
:malloc
			SET PUSH, B
			SET PUSH, C
			SET PUSH, X
			SET PUSH, Y
			SET X, 0
::mal_loop
			IFG X, memmask
				SET PC, mal_fail
			SET B, [memloc+X]
			SET C, B
			AND C, 0x8000
			AND B, 0x7fff
			IFN C, 0x0000
				SET PC, mal_loopiter
			IFG A, B
				SET PC, mal_loopiter
			IFE A, B
				SET PC, mal_loopiter
			SET PC, mal_loopend
::mal_loopiter
			ADD X, B
			SET PC, mal_loop
::mal_loopend
			ADD A, 2
			IFG B, A
				SUB A, 1
			BOR A, 0x8000
			SET [memloc+X], A
			AND A, 0x7999
			ADD A, 1
			IFG A, B
				SET PC, mal_getaddr
			SUB A, 1
			SET Y, X
			ADD Y, A
			SUB B, A
			SET [memloc+Y], B
::mal_getaddr
			SET A, X
			ADD A, meminit
			SET PC, mal_return
::mal_fail
			SET A, 0xffff
::mal_return
			SET Y, POP
			SET X, POP
			SET C, POP
			SET B, POP
			SET PC, POP
:free
			SET PUSH, B
			SET PUSH, C
			SET PUSH, X
			SET PUSH, Y
			SET PUSH, Z
			SET X, 0
			SUB A, meminit
::fre_loop
			SET B, [memloc+X]
			AND B, 0x7fff
			SET Z, X
			ADD Z, B
			IFE X, A
				SET PC, fre_loopend
			IFG X, A
				SET PC, fre_return
			SET Y, X
			SET X, Z
			IFG memlen, X
				SET PC, fre_loop
			SET PC, fre_return
::fre_loopend
			IFG memlen, Z
				JSR fre_mergeright
			IFG X, 0
				JSR fre_mergeleft
			SET [memloc+X], B
			SET PC, fre_return
::fre_mergeright
			SET C, [memloc+Z]
			AND C, 0x8000
			IFE C, 0x0000
				ADD B, [memloc+Z]
			SET PC, POP
::fre_mergeleft
			SET C, [memloc+Y]
			AND C, 0x8000
			IFN C, 0x0000
				SET PC, POP
			ADD B, [memloc+Y]
			SET X, Y
			SET PC, POP
::fre_return
			SET Z, POP
			SET Y, POP
			SET X, POP
			SET C, POP
			SET B, POP
			SET PC, POP
:memset
			SUB A, memloc
			ADD C, A
			SUB C, 1
::mem_loop
			IFG A, C
				SET PC, POP
			SET [memloc+A], B
			ADD A, 1
			SET PC, mem_loop
:__memory__skip__
